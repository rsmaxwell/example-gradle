
plugins {
    id 'application'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation libs.junit.jupiter

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation libs.guava
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

application {
    mainClass = 'org.example.App'
}

tasks.named('test') {
    useJUnitPlatform()
}

ext {
    buildId = (findProperty('buildID') ?: System.getenv('BUILD_ID'))?.toString()

    repositoryName = findProperty('repositoryName') ?: repositoryName_default

    baseVersion = findProperty('projectVersion') ?: projectVersion_default
    projectVersion = buildId ? "${baseVersion}-build-${buildId}" : baseVersion

    projectArtifactId = projectArtifactId_default

    mavenUser = findProperty('mavenUser') ?: System.getenv('MAVEN_USER')
    mavenPassword = findProperty('mavenPassword') ?: System.getenv('MAVEN_PASSWORD')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = projectArtifactId
            groupId = projectGroupId
            version = projectVersion
            from components.java
        }
    }

    repositories {
        maven {
            url = uri("${mavenRepositoryBaseURL}/${repositoryName}")
            credentials {
                username = project.ext.mavenUser
                password = project.ext.mavenPassword
            }
        }
    }
}

// Only enforce creds for publishing tasks:
tasks.withType(PublishToMavenRepository).configureEach {
    doFirst {
        if (!project.ext.mavenUser || !project.ext.mavenPassword) {
            throw new GradleException("Missing Maven credentials. Set mavenUser/mavenPassword or MAVEN_USER/MAVEN_PASSWORD.")
        }
    }
}